apply plugin: 'com.android.application'

def config = rootProject.ext

android {
    compileSdkVersion config.android.compileSdkVersion

    defaultConfig {
        applicationId config.appId.app
        minSdkVersion config.android.minSdkVersion
        targetSdkVersion config.android.targetSdkVersion
        versionCode config.android.versionCode
        versionName config.android.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    signingConfigs {
        sign {
            keyAlias 'hbuilder'
            keyPassword '123456'
            storeFile file('C:/Users/Administrator/Desktop/H5Sign/HBuilder.jks')
            storePassword '123456'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.sign
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

/*使用AspectJ的编译器*/
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.8'
        classpath 'org.aspectj:aspectjweaver:1.8.8'
    }
}

dependencies {
    if (!config.isModule) {
        api project(':module_mine')
        api project(':module_login')
        api project(':module_source')
        api project(':module_message')
    }
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
}

import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

final def variants = project.android.applicationVariants

variants.all { variant ->

    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.8",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]

        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)
    }
}
